/*
 * Copyright 2017-2023 Cyface GmbH
 *
 * This file is part of the Cyface SDK for Android.
 *
 * The Cyface SDK for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface SDK for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface SDK for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Top-level build file where you can add configuration options common to all sub-projects/modules.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 * @version 2.10.0
 * @since 1.0.0
 */

buildscript {
    ext.gradle_version = "7.4.2" // Major release available but breaks publish [RFR-412]
    ext.kotlin_version = "1.8.22" // Build fails when upgrading to 1.9.0/10

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    // To generate Proto DataStore
    id 'com.google.protobuf' version '0.8.19' apply false // Maybe keep in sync with other usages
}

ext {
    // This libraries version
    cyfaceAndroidBackendVersion = "0.0.0" // Automatically overwritten by CI

    // Cyface dependencies
    cyfaceUtilsVersion = "4.0.4"
    cyfaceSerializationVersion = "3.0.0"
    cyfaceUploaderVersion = "1.0.0"

    // Android SDK versions
    minSdkVersion = 21 // device support
    targetSdkVersion = 33 // behavioral changes, follow migration guide & test the app against this
    compileSdkVersion = 34 // allows newest APIs to be used and to see deprecations, use latest
    buildToolsVersion = '34.0.0' // optional, if defined, use latest (SDK Manager > SDK Tools)
    // 1.1.0-alpha05: ':persistence:mergeExtDexDebugAndroidTest' fails (transitive dependency)
    datastoreVersion = "1.1.0-alpha04" // only 1.1.0 supports multi-process datastore
    desugaringVersion = "2.0.3"

    // Android dependencies
    androidxAnnotationVersion = '1.6.0'
    androidxAppCompatVersion = "1.6.1"
    localbroadcastmanagerVersion = '1.1.0'
    roomVersion = "2.5.2"

    // Kotlin components
    coroutinesVersion = "1.6.4"

    // Other dependencies
    appAuthVersion = "0.11.1" // Can't move to uploader lib because of dependency issues
    protobufVersion = '3.22.2' // For Proto Datastore. Maybe keep in sync with other versions.

    // Testing
    junitVersion = "1.1.5"
    mockitoVersion = '5.2.0'
    mockitoKotlinVersion = '4.1.0'
    hamcrestVersion = "1.3"
    rulesVersion = "1.5.0"
    robolectricVersion = "4.10.2"
    androidxTestCoreVersion = "1.4.0"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    kotlinTargetJavaVersion = "11"

    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
}

allprojects {
    repositories {
        // Needed to resolve newest support libraries with IntelliJ / Gradle
        google()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url = uri("https://maven.pkg.github.com/cyface-de/android-utils")
            // Needed even for public projects: https://github.community/t5/GitHub-API-Development-and/Download-from-Github-Package-Registry-without-authentication/m-p/35255
            credentials {
                username = project.findProperty("githubUser")
                password = project.findProperty("githubToken")
            }
        }
        maven {
            url = uri("https://maven.pkg.github.com/cyface-de/uploader")
            // Needed even for public projects: https://github.community/t5/GitHub-API-Development-and/Download-from-Github-Package-Registry-without-authentication/m-p/35255
            credentials {
                username = project.findProperty("githubUser")
                password = project.findProperty("githubToken")
            }
        }
    }

    // Uncomment this for additional warnings.
    /*gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }*/
}

apply plugin: 'android-reporting' // must be at the end, not working with `plugins {}`

// To publish all sub-projects
task publishAll() {
    subprojects.each { pr ->
        dependsOn {
            pr.tasks.findAll { task -> task.name.startsWith('publish') }
        }
    }
    outputs.upToDateWhen { false }
}
publishAll.outputs.upToDateWhen { false }
