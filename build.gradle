/*
 * Copyright 2017-2025 Cyface GmbH
 *
 * This file is part of the Cyface SDK for Android.
 *
 * The Cyface SDK for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface SDK for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface SDK for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Top-level build file where you can add configuration options common to all sub-projects/modules.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 */

buildscript {
    ext.gradle_version = "8.2.2"
    ext.kotlin_version = "1.9.22"

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    // To generate Proto DataStore
    id 'com.google.protobuf' version '0.9.4' apply false // Maybe keep in sync with other usages
}

ext {
    // This libraries version
    cyfaceAndroidBackendVersion = "0.0.0" // Automatically overwritten by CI

    // Cyface dependencies
    cyfaceUtilsVersion = "4.2.0"
    // (!) Match versions `android-app`, `SDK` and `camera-service`.
    // When `android-app` is checkout out, `rootProject.uploaderVersion` in the submodules will point
    // to `android-app/build.gradle` instead of `submodule/build.gradle`.
    cyfaceSerializationVersion = "3.4.2"
    cyfaceUploaderVersion = "1.5.1"

    // Android SDK versions
    minSdkVersion = 21 // device support
    targetSdkVersion = 34 // behavioral changes, follow migration guide & test the app against this
    compileSdkVersion = 34 // allows newest APIs to be used and to see deprecations, use latest

    // Android dependencies
    desugaringVersion = "2.0.3"
    androidxAnnotationVersion = '1.6.0'
    androidxAppCompatVersion = "1.7.0"
    localbroadcastmanagerVersion = '1.1.0'
    roomVersion = "2.6.1"
    datastoreVersion = "1.1.1"

    // Kotlin components
    coroutinesVersion = "1.7.3"

    // Other dependencies
    appAuthVersion = "0.11.1" // Can't move to uploader lib because of dependency issues
    protobufVersion = '3.22.2' // For Proto Datastore. Maybe keep in sync with other versions.

    // Testing
    junitVersion = "1.2.1"
    mockitoVersion = '5.7.0'
    mockitoKotlinVersion = '5.2.1'
    hamcrestVersion = "1.3"
    rulesVersion = "1.6.1"
    robolectricVersion = "4.10.2"
    androidxTestCoreVersion = "1.4.0"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    kotlinTargetJavaVersion = "17"

    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
}

allprojects {
    repositories {
        // Needed to resolve newest support libraries with IntelliJ / Gradle
        google()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url = uri("https://maven.pkg.github.com/cyface-de/android-utils")
            // Needed even for public projects: https://github.community/t5/GitHub-API-Development-and/Download-from-Github-Package-Registry-without-authentication/m-p/35255
            credentials {
                username = project.findProperty("githubUser")
                password = project.findProperty("githubToken")
            }
        }
        maven {
            url = uri("https://maven.pkg.github.com/cyface-de/uploader")
            // Needed even for public projects: https://github.community/t5/GitHub-API-Development-and/Download-from-Github-Package-Registry-without-authentication/m-p/35255
            credentials {
                username = project.findProperty("githubUser")
                password = project.findProperty("githubToken")
            }
        }
    }

    // Uncomment this for additional warnings.
    /*gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }*/
}

apply plugin: 'android-reporting' // must be at the end, not working with `plugins {}`

// To publish all sub-projects
tasks.register('publishAll') {
    outputs.upToDateWhen { false }
}
gradle.projectsEvaluated {
    subprojects.each { pr ->
        //noinspection ConfigurationAvoidance
        pr.tasks.matching { task -> task.name.startsWith('publish') }.all { publishTask ->
            tasks.named('publishAll').configure { dependsOn publishTask }
        }
    }
}
