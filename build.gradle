/*
 * Copyright 2017 Cyface GmbH
 *
 * This file is part of the Cyface SDK for Android.
 *
 * The Cyface SDK for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface SDK for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface SDK for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Top-level build file where you can add configuration options common to all sub-projects/modules.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 * @version 2.7.5
 * @since 1.0.0
 */

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
    }
}

ext {
    // This libraries version
    cyfaceBackendVersion = "0.0.0" // Only increase in release branch. Not merged back to `main`

    // Cyface dependencies
    cyfaceUtilsVersion = "2.0.2"

    minSdkVersion = 21
    targetSdkVersion = 30
    compileSdkVersion = 30
    buildToolsVersion = '30.0.3'

    androidxAnnotationVersion = "1.0.0"
    androidxAppCompatVersion = "1.2.0"
    localbroadcastmanagerVersion = "1.0.0"

    // testing
    junitVersion = "1.1.2"
    mockitoVersion = "3.3.3"
    hamcrestVersion = "1.3"
    rulesVersion = "1.3.0"
    robolectricVersion = "4.5.1"
    androidxTestCoreVersion = "1.1.0"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
}

// Load credentials
def properties = new Properties()
properties.load(new FileInputStream("local.properties"))

allprojects {
    repositories {
        // Needed to resolve newest support libraries with IntelliJ / Gradle
        google()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url = uri("https://maven.pkg.github.com/cyface-de/android-utils")
            // Needed even for public projects: https://github.community/t5/GitHub-API-Development-and/Download-from-Github-Package-Registry-without-authentication/m-p/35255
            credentials {
                username = properties.getProperty("github.user")
                password = properties.getProperty("github.token")
            }
        }
    }

    // Uncomment this for additional warnings.
    /*gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }*/
}

apply plugin: 'android-reporting'

// To publish all sub-projects
task publishAll() {
    subprojects.each { pr ->
        dependsOn {
            pr.tasks.findAll { task -> task.name.startsWith('publish') }
        }
    }
    outputs.upToDateWhen { false }
}
publishAll.outputs.upToDateWhen { false }

// Auto-generated by Android Studio
task clean(type: Delete) {
    delete rootProject.buildDir
}

// The gradle publish tasks is defined in a separate file:
apply from: 'publish.gradle'
