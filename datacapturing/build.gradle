/*
 * Copyright 2017-2025 Cyface GmbH
 *
 * This file is part of the Cyface SDK for Android.
 *
 * The Cyface SDK for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface SDK for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface SDK for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Gradle's build file for the module which captures the sensor data.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 * @version 3.5.0
 * @since 1.0.0
 */

plugins {
    // use `:datacapturing:publishToMavenLocal` instead of `publishToMavenLocal` (see custom task `publishAll`)
    // Don't move to root `build.gradle` as this forces SDK and camera_service to define this, too.
    // Keep version in sync with other modules!
    id 'de.cyface.android-publish' version '1.1.0'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version '2.0.21-1.0.28' // Upgrade together with `kotlin_version`
    id 'androidx.room'
}

android {
    namespace "de.cyface.datacapturing"
    compileSdk = rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName rootProject.ext.cyfaceAndroidBackendVersion
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner

        // Placeholders for AndroidManifest.xml
        manifestPlaceholders = [
                // Dummy schema, required by the AppAuth dependency.
                // Replace this in the app which in integrated the SDK.
                'appAuthRedirectScheme': 'com.example.PLACEHOLDER'
        ]
    }

    room {
        // Write Room database schema to file to record database schema changes in version control
        schemaDirectory("$projectDir/schemas")
    }

    buildTypes {
        release {
            // Enable in app module to generate a single mapping.xml which can be uploaded to PlayStore
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Required by Roboletric
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    compileOptions {
        // Enabling desugaring to support Java 8 and Java 11 features
        coreLibraryDesugaringEnabled true
        // Set Java compatibility
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.kotlinTargetJavaVersion
    }

    // Avoid `2 files found with path ...` error
    packagingOptions {
        exclude("META-INF/DEPENDENCIES")
    }

    lintOptions {
        // To ignore false-positive lint errors
        baseline file("lint-baseline.xml")
    }

    buildFeatures { // Required with AGP 8+
        buildConfig = true
    }
}

dependencies {
    // Android dependencies
    implementation "androidx.annotation:annotation:$rootProject.ext.androidxAnnotationVersion"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:$rootProject.ext.localbroadcastmanagerVersion"
    // Add support desugaring with for NIO
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs_nio:$rootProject.ext.desugaringVersion"

    // `Room` API used to access database
    // Further room libraries and Kotlin support see Room guide.
    implementation "androidx.room:room-runtime:$rootProject.ext.roomVersion"
    ksp "androidx.room:room-compiler:$rootProject.ext.roomVersion"

    // Kotlin components
    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$rootProject.ext.coroutinesVersion"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.coroutinesVersion"

    // Cyface dependencies
    api "de.cyface:android-utils:$rootProject.ext.cyfaceUtilsVersion"
    implementation project(":persistence")
    implementation project(":synchronization")

    // Dependencies for instrumentation tests
    androidTestImplementation project(":testutils")
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"

    // Dependencies for local unit tests
    testImplementation project(":testutils")
    // If Junit symbols are unresolvable make sure Build Variant is set to debug
    testImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    // Optional - For better debuggable asserts
    testImplementation "org.hamcrest:hamcrest:$rootProject.ext.hamcrestVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
}
