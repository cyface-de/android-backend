/**
 * Gradle's build file for the module which captures the sensor data.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 * @version 2.0.2
 * @since 1.0.0
 */

apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        versionCode 1 // this must be incremented if new apk should be pushed to play store
        // this is shown to the users, see Confluence/Android Client for naming guideline
        versionName rootProject.ext.version

        testInstrumentationRunner rootProject.ext.testInstrumentationRunner

        missingDimensionStrategy 'full', 'mock'

            // Defines UI elements for Notification Element
            buildConfigField "int", "NOTIFICATION_LOGO", "R.drawable.ic_movebis_notification"
            buildConfigField "int", "NOTIFICATION_TITLE", "R.string.notification_title"
            buildConfigField "int", "NOTIFICATION_TEXT", "R.string.capturing_active"
            buildConfigField "int", "NOTIFICATION_CHANNEL", "R.string.notification_channel_id"
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    // Specifies the flavor dimensions you want to use. The order in which you
    // list each dimension determines its priority, from highest to lowest,
    // when Gradle merges variant sources and configurations. You must assign
    // each product flavor you configure to one of the flavor dimensions.
    flavorDimensions "project", "mode"

    productFlavors {
        // Configurations in the "project" product flavors override those in "mode"
        // flavors and the defaultConfig block. Gradle determines the priority
        // between flavor dimensions based on the order in which they appear next
        // to the flavorDimensions property above--the first dimension has a higher
        // priority than the second, and so on.
        movebis {
            dimension "project"
            versionNameSuffix "-full"
        }
        cyface {
            dimension "project"
            versionNameSuffix "-cyface"
        }

        mock {
            dimension "mode"
        }

        full {
            dimension "mode"
        }
    }

    // Enable Proguard for the release build type.
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:$rootProject.ext.androidxAnnotationVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.androidxAppCompatVersion"
    implementation project(":utils")
    implementation project(":persistence")
    implementation project(":synchronization")

    // Dependencies for instrumentation tests
    androidTestImplementation project(":testutils")
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.extJunitVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"

    // Dependencies for local unit tests
    // - If Junit symbols are not resolvable in IntelliJ, make sure Build Variant is set to debug
    // - Loading another dependency (e.g. module) only it's production dependencies (compile) are loaded but not other dependencies (e.g. testCompile)
    //implementation 'com.android.support:appcompat-v7:' + rootProject.ext.junitVersion - auto generated by Android Studio
    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    // Optional - For better debuggable asserts
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
}
