/*
 * Copyright 2017-2021 Cyface GmbH
 *
 * This file is part of the Cyface SDK for Android.
 *
 * The Cyface SDK for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface SDK for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface SDK for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Gradle's build file for the module which persists the sensor data.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 * @version 3.1.0
 * @since 1.0.0
 */

plugins {
    id 'com.android.library'
    id 'com.google.protobuf' version '0.8.16'
    // To download the `measurement.proto` file and to generate `java-lite` files required here
    //noinspection SpellCheckingInspection
    id "de.undercouch.download" version "4.1.1"
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName rootProject.ext.cyfaceBackendVersion
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner
    }

    buildTypes {
        release {
            // Enable in app module to generate a single mapping.xml which can be uploaded to PlayStore
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // To avoid having to upgrade to `androidX.appCompat:1.3.0-beta1` [CY-5525]
    lintOptions {
        disable 'ObsoleteLintCustomCheck'
    }

    // Uncomment to show more lint warnings
    /*tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }*/

    // To add the `measurement.proto` file (downloadAndUnzipProto task) to the sourceSet
    sourceSets {
        main {
            proto {
                // In addition to the default 'src/main/proto'
                srcDir "build/protos-0.0.3/src/main/proto"
            }
        }
        test {
            proto {
                srcDir "build/protos-0.0.3/src/main/proto"
            }
        }
    }
}

dependencies {
    // Protocol Buffer: Download proto file from Cyface `protos` repo
    // https://github.com/google/protobuf-gradle-plugin#protos-in-dependencies
    // `protobuf` instead of `implementation` as we want to re-compile the proto files
    //protobuf "de.cyface:protos:$rootProject.ext.cyfaceProtosVersion"
    // `protobuf` loads both, our proto and the google proto -> collision
    // `implementation` also loads the java classes, which we don't want
    // instead of importing our proto file  via dependencies which fails because it
    // includes the google proto files which collide with the dependency of java-lite
    // we probably download the zip, extract the proto file of ours and include the sourceSets
    // But document this and open an issue in gradle protobuf plugin repo

    // Protocol Buffer: generate java files
    implementation "com.google.protobuf:protobuf-javalite:$rootProject.ext.protobufVersion"
    implementation "androidx.annotation:annotation:$rootProject.ext.androidxAnnotationVersion"

    // Cyface dependencies
    implementation "de.cyface:android-utils:$rootProject.ext.cyfaceUtilsVersion"

    // Utility
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion" // Using Validate

    // Dependencies for local unit tests
    testImplementation project(":testutils")
    // If Junit symbols are unresolvable make sure Build Variant is set to debug
    testImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    // Optional - For better debuggable asserts
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
    testImplementation "androidx.test:core:$rootProject.ext.androidxTestCoreVersion"

    // Dependencies for instrumentation tests
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"
}

// Gradle Protocol Buffer Plugin: https://github.com/google/protobuf-gradle-plugin
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    generateProtoTasks {
        //noinspection GroovyAssignabilityCheck
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

// Download the `measurement.proto` file to compile the `java-lite` proto classes
task downloadProto(type: Download) {
    src "https://github.com/cyface-de/protos/archive/refs/tags/${cyfaceProtosVersion}.zip"
    dest new File(buildDir, "protos-${cyfaceProtosVersion}.zip")
}
task downloadAndUnzipProto(dependsOn: downloadProto, type: Copy) {
    from zipTree(downloadProto.dest)
    into buildDir
}
preBuild.dependsOn downloadAndUnzipProto
