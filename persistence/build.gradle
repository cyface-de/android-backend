/*
 * Copyright 2017-2023 Cyface GmbH
 *
 * This file is part of the Cyface SDK for Android.
 *
 * The Cyface SDK for Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Cyface SDK for Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Cyface SDK for Android. If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * Gradle's build file for the module which persists the sensor data.
 *
 * @author Armin Schnabel
 * @author Klemens Muthmann
 * @version 3.5.0
 * @since 1.0.0
 */

plugins {
    // use `:datacapturing:publishToMavenLocal` instead of `publishToMavenLocal` (see custom task `publishAll`)
    // Don't move to root `build.gradle` as this forces SDK and camera_service to define this, too.
    // Keep version in sync with other modules!
    id 'de.cyface.android-publish' version '1.0.5'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    namespace "de.cyface.persistence"
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName rootProject.ext.cyfaceAndroidBackendVersion
        testInstrumentationRunner rootProject.ext.testInstrumentationRunner

        // Write Room database schema to file to record database schema changes in version control
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            // Enable in app module to generate a single mapping.xml which can be uploaded to PlayStore
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // Enabling desugaring to support Java 8 and Java 11 features
        coreLibraryDesugaringEnabled true
        // Set Java compatibility
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

    kotlinOptions {
        jvmTarget = rootProject.ext.kotlinTargetJavaVersion
    }

    sourceSets {
        // Adds exported schemas as test assets
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    lintOptions {
        // To ignore false-positive lint errors
        baseline file("lint-baseline.xml")
    }

    // Uncomment to show more lint warnings
    /*tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }*/
}

dependencies {
    // Android dependencies
    implementation "androidx.annotation:annotation:$rootProject.ext.androidxAnnotationVersion"
    // Add support desugaring with for NIO
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs_nio:$rootProject.ext.desugaringVersion"

    // `Room` API used to access database
    // Further room libraries and Kotlin support see Room guide.
    implementation "androidx.room:room-runtime:$rootProject.ext.roomVersion"
    // To observe data with `Flow`
    implementation "androidx.room:room-ktx:$rootProject.ext.roomVersion"
    // To use Room with Kotlin
    kapt "androidx.room:room-compiler:$rootProject.ext.roomVersion"

    // Cyface dependencies
    implementation "de.cyface:android-utils:$rootProject.ext.cyfaceUtilsVersion"
    api "de.cyface:serializer:$rootProject.ext.cyfaceSerializationVersion"

    // Dependencies for local unit tests
    testImplementation project(":testutils")
    // If Junit symbols are unresolvable make sure Build Variant is set to debug
    testImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    // Optional - For better debuggable asserts
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.ext.robolectricVersion"
    testImplementation "androidx.test:core:$rootProject.ext.androidxTestCoreVersion"

    // Dependencies for instrumentation tests
    androidTestImplementation project(":testutils")
    androidTestImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"
    androidTestImplementation "androidx.room:room-testing:$rootProject.ext.roomVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitVersion"
    androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"
}
